plugins {
    id 'java'
}

group 'io.github.johnfg10'
version '1.0.0'

repositories {
    mavenCentral()
}

def vertxVersion = '3.5.2'

dependencies {
    // vertx
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"
    compile "io.vertx:vertx-codegen:$vertxVersion"

    // impl
    compile 'com.vladsch.flexmark:flexmark-all:0.34.2'
    compile 'com.atlassian.commonmark:commonmark:0.11.0'

    // tests
    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile group: 'junit', name: 'junit', version: '4.12'

}


def generatedSrcPath="$buildDir/generated-src"
def generatedSrcDir = file(generatedSrcPath)

sourceSets {
    main {
        java.srcDirs += generatedSrcDir
        output.dir(builtBy: 'generateServiceProxy', generatedSrcPath)
    }
}

task generateServiceProxy(type: JavaCompile, description: 'Generates EBServiceProxies') {
    source = sourceSets.main.java
    classpath = configurations.compile
    destinationDir = generatedSrcDir
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-AoutputDirectory=$generatedSrcPath"
    ]
}

compileJava.dependsOn generateServiceProxy

clean {
    delete += generatedSrcDir
}